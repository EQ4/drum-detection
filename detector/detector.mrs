Series {
  ~ DeltaRatio: "delta_ratio.mrs"

  + public input = ""
  + public output = ""
  + public win_size = 2048
  + public hop_size = 1024
  + public delta_threshold = 0.5
  + public peak_threshold = 2.0

  + done = (sndfile/hasData == false)

  inSamples = hop_size

  -> sndfile: SoundFileSource { filename = /input }

  -> ShiftInput { winSize = (2 * /inSamples) } -> Windowing

  -> Spectrum -> PowerSpectrum

  -> Fanout
  {
    -> Series
    {
        -> DeltaRatio
        -> Threshold { value = /delta_threshold }
        -> Memory { memSize = 18 }
        -> PeakerOnset
        {
          lookAheadSamples = 4
          threshold = /peak_threshold
        }
    }

    -> Series {
      -> Memory{memSize=3}
      -> Sum { mode = "sum_observations" }
      -> Centroid
      -> DelaySamples{delay=2}
    }
  }

  -> CsvSink { filename = /output separator = ", " }
}
