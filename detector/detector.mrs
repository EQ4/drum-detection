Series {
  + public input = ""
  + public output = ""
  + public win_size = 1024
  + public hop_size = 512
  + public peak_threshold = 1.7

  + done = (sndfile/hasData == false)

  inSamples = hop_size

  -> sndfile: SoundFileSource { filename = /input }
  -> MixToMono

  -> ShiftInput { winSize = (2 * /inSamples) }

  -> Sidechain {
    -> Series {
      -> Rms
      // three samples, with the center one matching the onset
      -> DelaySamples { delay = 2 } -> Memory { memSize = 3 }
      -> MaxMin // sample 0 = max, sample 1 = min
      -> rms: FlowToControl
    }
  }

  -> Windowing
  -> Spectrum

  -> Fanout
  {
    -> onsets: "onsets.mrs"
    {
      peak_threshold = /peak_threshold
    }

    -> Series
    {
      -> PowerSpectrum
      -> Memory{memSize=3}
      -> Sum { mode = "sum_observations" }
      -> Centroid
      -> DelaySamples{delay=2}
    }
  }

  -> CsvSink { filename = /output separator = ", " }
}
